<html>
    <!-- vertex Shader -->
    <script id="vertex-shader" type="x-shader/x-vertex">
        #version 300 es
        precision mediump float;

        uniform mat4 uModelViewMatrix;
        uniform mat4 uProjectionMatrix;
        uniform mat4 uNormalMatrix;

        in vec3 aVertexPosition;
        in vec3 aVertexNormal;

        out vec3 vNormal;
        out vec3 vEyeVector;

        void main(void) {
          vec4 vertex = uModelViewMatrix * vec4(aVertexPosition, 1.0);
          vNormal = vec3(uNormalMatrix * vec4(aVertexNormal, 1.0));
          vEyeVector = -vec3(vertex.xyz);
          gl_Position = uProjectionMatrix * uModelViewMatrix * vec4(aVertexPosition, 1.0);
        }
  </script>

  <!-- fragment Shader -->
  <script id="fragment-shader" type="x-shader/x-fragment">
        #version 300 es
        precision mediump float;

        uniform float uShininess;
        uniform vec3 uLightDirection;
        uniform vec4 uLightAmbient;
        uniform vec4 uLightDiffuse;
        uniform vec4 uLightSpecular;
        uniform vec4 uMaterialAmbient;
        uniform vec4 uMaterialDiffuse;
        uniform vec4 uMaterialSpecular;

        in vec3 vNormal;
        in vec3 vEyeVector;

        out vec4 fragColor;

        void main(void) {
          vec3 L = normalize(uLightDirection);

          vec3 N = normalize(vNormal);

          float lambertTerm = dot(N, -L);
          // Ambient
          vec4 Ia = uLightAmbient * uMaterialAmbient;
          // Diffuse
          vec4 Id = vec4(0.0, 0.0, 0.0, 1.0);
          // Specular
          vec4 Is = vec4(0.0, 0.0, 0.0, 1.0);

          if (lambertTerm > 0.0) {
            Id = uLightDiffuse * uMaterialDiffuse * lambertTerm;
            vec3 E = normalize(vEyeVector);
            vec3 R = reflect(L, N);
            float specular = pow( max(dot(R, E), 0.0), uShininess);
            Is = uLightSpecular * uMaterialSpecular * specular;
          }

          fragColor = vec4(vec3(Ia + Id + Is), 1.0);
        }
  </script>
    <body>
        <div id="container">
            <canvas id="canvas"></canvas>
        </div>
        <script type="text/javascript" src="bunny.js"></script>
        <script type="text/javascript" src="app.js"></script>
    </body>
</html>